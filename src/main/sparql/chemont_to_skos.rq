##sparql --results=TURTLE --data=/home/gehau/Desktop/ChemOnt_2_1.ttl  --query chemont_to_skos.rq | sed -e 's;http://purl.obolibrary.org/obo/;https://data.omgeving.vlaanderen.be/id/concept/chemische_stof/;'> /home/gehau/git/codelijst-chemische_stof/src/main/resources/be/vlaanderen/omgeving/data/id/conceptscheme/chemische_stof/skos_chemont_2_1.ttl
prefix skos:  <http://www.w3.org/2004/02/skos/core#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix dbo:   <http://dbpedia.org/ontology/>
prefix cs:    <https://data.vlaanderen.be/ns/chemische_stof#>
prefix dbp:   <http://dbpedia.org/property/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix csc: <http://purl.obolibrary.org/obo/>
prefix vlcs:  <https://data.omgeving.vlaanderen.be/id/conceptscheme/>

construct

{
?s a skos:Concept ;
skos:broader ?broader ;
skos:prefLabel ?label ;
skos:definition ?definition ;
skos:altLabel ?altLabel ;
skos:notation ?notation ;
skos:inScheme   vlcs:chemische_stof ;
.
?broader skos:narrower ?s ;
.
}

where

{
?s a owl:Class ;

rdfs:label ?label ;

.
optional {?s rdfs:subClassOf ?broader }
optional {?s <http://purl.obolibrary.org/obo/IAO_0000115> ?definition }
optional {?s <http://www.geneontology.org/formats/oboInOwl#hasExactSynonym> ?altLabel }
optional { ?s <http://www.geneontology.org/formats/oboInOwl#id> ?notation }
optional {?s <http://www.geneontology.org/formats/oboInOwl#hasRelatedSynonym> ?altLabel }

}