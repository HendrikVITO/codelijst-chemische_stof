##sparql --results=TURTLE --data=/home/gehau/Desktop/ChemOnt_2_1.ttl  --query chemont_to_skos.rq | sed -e 's;http://purl.obolibrary.org/obo/;https://data.omgeving.vlaanderen.be/id/concept/chemische_stof/;'> /home/gehau/git/codelijst-chemische_stof/src/main/resources/be/vlaanderen/omgeving/data/id/conceptscheme/chemische_stof/skos_chemont_2_1.ttl
prefix skos:  <http://www.w3.org/2004/02/skos/core#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix dbo:   <http://dbpedia.org/ontology/>
prefix cs:    <https://data.vlaanderen.be/ns/chemische_stof#>
prefix dbp:   <http://dbpedia.org/property/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix csc: <http://purl.obolibrary.org/obo/>
prefix vlcs:  <https://data.omgeving.vlaanderen.be/id/conceptscheme/>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct

{
?s a skos:Concept ;
skos:broader ?broader ;
skos:prefLabel ?label ;
skos:definition ?definition ;
skos:altLabel ?altLabel ;
skos:notation ?notation ;
skos:inScheme   vlcs:chemische_stof ;
.

}

where

{
?s a owl:Class ;

rdfs:label ?la ;
.
BIND(STRLANG(?la, "en") AS ?label)
optional {?s rdfs:subClassOf ?broader . FILTER( ?broader != <http://purl.obolibrary.org/obo/CHEMONTID_9999999> ) }
optional {?s <http://purl.obolibrary.org/obo/IAO_0000115> ?def . BIND(STRLANG(?def, "en") AS ?definition) }
optional {?s <http://www.geneontology.org/formats/oboInOwl#hasExactSynonym> ?al2 .
            BIND(STRLANG(?al2, "en") AS ?altLabel)
            FILTER NOT EXISTS { ?s rdfs:label ?al2}
}
optional { ?s <http://www.geneontology.org/formats/oboInOwl#id> ?notation }
optional {?s <http://www.geneontology.org/formats/oboInOwl#hasRelatedSynonym> ?al .
            BIND(STRLANG(?al, "en") AS ?altLabel)
            FILTER NOT EXISTS { ?s rdfs:label ?al}
            }

#BIND(STRLANG(?la, "en") AS ?label)

FILTER( ?s != <http://purl.obolibrary.org/obo/CHEMONTID_9999999> )


}